plugins {
    id 'groovy'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

description = 'A Java/Groovy client for the CloudFlare v4 API'
group = 'com.mhackner'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            pom.withXml {
                asNode().appendNode('description', description)
                def scm = asNode().appendNode('scm')
                scm.appendNode('developerConnection', 'scm:git:https://github.com/HackAttack/cloudflare-java-client.git')
                scm.appendNode('url', 'https://github.com/HackAttack/cloudflare-java-client')
                scm.appendNode('tag', 'HEAD')
            }
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.4.7'
    compile 'org.codehaus.groovy:groovy-json:2.4.7'
    compile 'org.codehaus.groovy:groovy-xml:2.4.7'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
    compile 'com.google.guava:guava:21.0'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.tomakehurst:wiremock:2.2.1'
    testRuntime 'org.slf4j:slf4j-nop:1.7.16'
}
